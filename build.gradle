plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'org.sonarqube' version '4.4.1.3373'
	id 'com.diffplug.spotless' version '6.25.0'
	id "org.springdoc.openapi-gradle-plugin" version "1.9.0"
	id("co.uzzu.dotenv.gradle") version "4.0.0"
}

group = 'com.soat.fiap'
version = '0.0.1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven {
		name = "GitHubPackages"
		url = uri("https://maven.pkg.github.com/FIAP-SOAT-TECH-TEAM/foodcore-shared")
		credentials {
			username = System.getenv("GITHUB_USERNAME") ?: env.GITHUB_USERNAME.value
			password = System.getenv("GITHUB_TOKEN") ?: env.GITHUB_TOKEN.value
		}
	}
}

ext {
	set('mapstructVersion', "1.5.5.Final")
	set('springdocVersion', "2.8.4")
	set('springCloudVersion', "2024.0.0")
	set('retrofit', "3.0.0")
	set('converterGson', "3.0.0")
	set('azureStarterDataCosmos', "6.0.0")
	set('foodcoreShared', "0.0.1")
}

dependencies {
	// Github Packages
	implementation "com.soat.fiap:foodcore-shared:${foodcoreShared}"

	// Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'

	// Azure
	implementation "com.azure.spring:spring-cloud-azure-starter-data-cosmos:${azureStarterDataCosmos}"

	// Spring Cloud
	implementation 'org.springframework.cloud:spring-cloud-starter-kubernetes-discoveryclient'

	// Micrometer
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.micrometer:micrometer-tracing-bridge-brave'
	implementation 'io.zipkin.reporter2:zipkin-reporter-brave'

	// Lombok e MapStruct
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	// SpringDoc (Swagger/OpenAPI)
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:${springdocVersion}"

	// Testes
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Retrofit
	implementation "com.squareup.retrofit2:retrofit:${retrofit}"
	implementation "com.squareup.retrofit2:converter-gson:${converterGson}"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

spotless {
	java {
		eclipse().configFile('spotless-formatter.xml')
		removeUnusedImports()
		importOrder('java', 'javax', 'org', 'com', '')
		trimTrailingWhitespace()
		endWithNewline()
		target 'src/**/*.java'
		targetExclude '**/generated/**', '**/build/**'
		formatAnnotations()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}

openApi {
	apiDocsUrl.set("http://localhost:8085/api/v3/api-docs")
	outputDir.set(file("$buildDir/openapi"))
	outputFileName.set("swagger.json")
	customBootRun {
		args.set([
			"--spring.profiles.active=test"
		])
	}
}
